# Copyright 2023-2024 DreamWorks Animation LLC
# SPDX-License-Identifier: Apache-2.0

if(GLD STREQUAL "$ENV{STUDIO}")
    set(versionString $ENV{REZ_BUILD_PROJECT_VERSION})
endif()
if("" STREQUAL "${versionString}")
    set(version_regex [[^.*_?version[ ]*=[ ]*['"](.*)['"][ ]*$]])
    file(STRINGS package.py package_py_version REGEX ${version_regex})
    if(package_py_version MATCHES ${version_regex})
        string(REPLACE "." ";" versionList ${CMAKE_MATCH_1})
        list(LENGTH versionList numParts)
        while(${numParts} LESS 4)
            list(APPEND versionList "0")
            list(LENGTH versionList numParts)
        endwhile()
        list(JOIN versionList "." versionString)
    else()
        set(versionString "0.0.0.0")
    endif()
endif()

set(name_regex [[^.*name[ ]*=[ ]*['"](.*)['"][ ]*$]])
file(STRINGS package.py package_py_name REGEX ${name_regex})
if(package_py_name MATCHES ${name_regex})
    set(PACKAGE_NAME ${CMAKE_MATCH_1})
else()
    cmake_path(GET CMAKE_CURRENT_SOURCE_DIR FILENAME PACKAGE_NAME)
endif()
string(REPLACE "_" ";" name_words ${PACKAGE_NAME})
foreach(word IN LISTS name_words)
    string(SUBSTRING ${word} 0 1 first_letter)
    string(TOUPPER ${first_letter} first_letter)
    string(REGEX REPLACE "^.(.*)" "${first_letter}\\1" word "${word}")
    list(APPEND cap_words ${word})
endforeach()
list(JOIN cap_words "" projectString)
